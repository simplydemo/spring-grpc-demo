plugins {
    id 'com.google.protobuf' version '0.8.18'
    id 'maven-publish'
    id 'java'
}

group = 'demo.example'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'

ext {
    set("grpcVersion", "1.42.2")
    set("protobufVersion", "3.20.1")
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    // compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.0'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    runtimeOnly "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generatedFilesBaseDir = "$projectDir/build/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}